syntax top:File = body:Block {
  #scope (body)
  BExpression` #callcc (fun return.
    `t(body) return (fun _. #crash))
}

syntax var:Expression = id:Identifier {
  BExpression` `id(id)
}

syntax intLit:Expression = i:Integer {
  BExpression` `int(i)
}

syntax strLit:Expression = s:String {
  BExpression` `str(s)
}

// This has incorrect semantics when called with the wrong number of arguments
// Also breaks precedence transformation, f must be high precedence, e and es must be low precence :(
syntax funcCall:Expression =
  f:Expression "(" args:ArgList ")"
{
  #prec 15
  #scope (args)
  #scope (f)
  BExpression` `t(args) `t(f) #unit
}

// This is included to enable the Expressions to go to the top level Expression non-terminal, ideally it would be defined inline in funcCall, but then it would recurse into a lower precedence
syntax argList:ArgList =
  (e:Expression ("," es:Expression)*)?
{
  BExpression` efun f. `t(
    foldl es prev
      (BExpression` (#deref `t(prev)) `t(es))
    foldl e prev
      (BExpression` (#deref `t(prev)) `t(e))
    (BExpression` f))
}

syntax debugPrint:Expression = "#" "debug" {
  BExpression` fun x. fun _. #print x
}

syntax debugCrash:Statement = "#" "crash" {
  BExpression` #crash
}

syntax equal:Expression = a:Expression "==" b:Expression {
  #prec 8
  BExpression` #equal (#deref `t(a)) (#deref `t(b))
}

syntax plus:Expression = a:Expression "+" b:Expression {
  #prec 11
  #assoc left
  BExpression` #plus (#deref `t(a)) (#deref `t(b))
}

syntax minus:Expression = a:Expression "-" b:Expression {
  #prec 11
  #assoc left
  BExpression` #minus (#deref `t(a)) (#deref `t(b))
}

syntax times:Expression = a:Expression "*" b:Expression {
  #assoc left
  #prec 12
  BExpression` #multiply (#deref `t(a)) (#deref `t(b))
}

syntax divide:Expression = a:Expression "/" b:Expression {
  #assoc left
  #prec 12
  BExpression` #divide (#deref `t(a)) (#deref `t(b))
}

syntax parens:Expression = "(" e:Expression ")" {
  e
}

syntax funcCallStmnt:Statement =
  f:Expression "(" args:ArgList ")"
{
  #scope (f)
  #scope (args)
  BExpression` efun _. efun _. `t(args) `t(f) #unit
}

syntax assignment:Statement =
  ref:Expression "=" value:Expression
{
  BExpression` efun _. efun _.
    #assign `t(ref) (#deref `t(value))
}

syntax local:Statement =
  "local" x:Pattern ("=" e:Expression)?
{
  #scope (e)
  BExpression` efun _. efun _.
    (fun expr. #callcc (fun done.
      (#callcc (fun fail. (`t(x) fail expr; done #unit));
       `t(x) (fun _. #crash) (#deref expr))))
    (#ref (#deref `t(foldr e _ (e) (BExpression` #unit))))
}

syntax block:Statement = "do" b:Block "end" {
  b
}

syntax if:Statement =
  "if" cond:Expression
  "then" then:Block
  elseif:("elseif" econd:Expression "then" ethen:Block)*
  ("else" else:Block)? "end"
{
  #scope (cond (then))
  #scope elseif:(econd (ethen))
  #scope (else)

  BExpression` efun return. efun break. #if (#deref `t(cond))
    (fun _. `t(then) return break)
    (fun _. `t(
      foldr elseif next
        (BExpression` #if (#deref `t(econd))
          (fun _. `t(ethen) return break)
          (fun _. `t(next)))
      foldr else _
        (BExpression` `t(else) return break)
      (BExpression` #unit)))
}

syntax while:Statement =
  "while" cond:Expression "do"
  body:Block "end"
{
  #scope (cond (body))
  BExpression` efun return. efun _. #callcc (fun break.
    #fix (fun repeat. fun _.
      #if (#deref `t(cond))
        (fun _.
          (`t(body) return break;
           repeat #unit))
        (fun _. #unit)))
}

syntax repeat:Statement =
  "repeat" body:Block "until" cond:Expression
{
  #scope ((body) cond)
  BExpression` efun return. efun _. #callcc (fun break.
    #fix (fun repeat. fun _. (
      `t(body) return break;
      #if (#deref `t(cond))
        (fun _. repeat #unit)
        (fun _. #unit))))
}

syntax numFor:Statement =
  "for" i:Pattern "="
  s:Expression "," e:Expression
  ("," step:Expression)?
  "do" body:Block "end"
{
  #scope (s)
  #scope (e)
  #scope (step)
  #scope (i (body))
  BExpression` efun return. efun _.
    (defAfter step = #deref
      `t(foldr step _ (step) (BExpression` 1));
     defAfter end = #deref `t(e);
     #callcc (fun break. #fix (fun rep. fun x.
       (`t(i) (fun _. #crash) x;
        #if (#leq x end)
          (fun _. (`t(body) return break;
                   rep (#plus x step)))
          (fun _. #unit)))
       `t(s)))
}

syntax function:Statement =
  "function" f:Identifier
  "(" (a:Pattern ("," as:Pattern)*)? ")"
  body:Block "end"
{
  #bind f before
  #bind f after
  #bind f in body
  #scope (a as (body))
  BExpression` efun _. efun _.
    (defAround `id(f) = #fix (fun recur.
      (defAfter `id(f) = recur;
       `t(foldr a next
            (BExpression` fun x.
              (`t(a) (fun _. #crash) x;
               `t(next)))
          foldr as next
            (BExpression` fun x.
              (`t(as) (fun _. #crash) x;
               `t(next)))
          (BExpression` fun _. #callcc (fun return.
            `t(body) return (fun _. #crash)))))))
}

syntax blockContent:Block =
  (stmnts:Statement ";"?)* ret:Return?
{
  #scope (stmnts ret)
  BExpression` efun return. efun break. `t(
    foldr stmnts next
      (BExpression` `t(stmnts) return break; `t(next))
    foldr ret _
      (BExpression` `t(ret) return break)
    (BExpression` #unit))
}

syntax return:Return = "return" e:Expression? {
  BExpression` efun return. efun break. `t(
    foldr e _
      (BExpression` return (#deref `t(e)))
    (BExpression` return #unit))
}

syntax break:Return = "break" {
  BExpression` efun return. efun break. break #unit
}

// These are included here copied, with no changes, to simulate the cherrypicking process

syntax intPat:Pattern = i:Integer {
  BExpression` efun fail. efun val.
    #if (#equal `int(i) val)
      (fun _. #unit)
      (fun _. fail #unit)
}
syntax floatPat:Pattern = f:Float {
  BExpression` efun fail. efun val.
    #if (#equal `float(f) val)
      (fun _. #unit)
      (fun _. fail #unit)
}
syntax stringPat:Pattern = s:String {
  BExpression` efun fail. efun val.
    #if (#equal `str(s) val)
      (fun _. #unit)
      (fun _. fail #unit)
}
syntax bindPat:Pattern = id:Identifier {
  #bind id after
  BExpression` efun _. efun val.
    defAfter `id(id) = val
}
syntax wildcardPat:Pattern = "_" {
  BExpression` efun _. efun _. #unit
}
syntax listPat:Pattern =
  "[" (p:Pattern (";" ps:Pattern)*)? "]"
{
  BExpression` efun fail. efun val.
    `t(foldr p next
         (BExpression` efun prev.
           (#if (#equal prev #nil)
              (fun _. fail #unit)
              (fun _. #unit);
            `t(p) fail (#head prev);
            `t(next) (#tail prev)))
       foldr ps next
         (BExpression` efun prev.
           (#if (#equal prev #nil)
              (fun _. fail #unit)
              (fun _. #unit);
            `t(ps) fail (#head prev);
            `t(next) (#tail prev)))
       (BExpression` (efun prev.
         #if (#equal prev #nil)
           (fun _. #unit)
           (fun _. fail #unit))))
    val
}
syntax consPat:Pattern = h:Pattern ":" ":" t:Pattern {
  #assoc right
  #prec 10
  BExpression` efun fail. efun value.
    (#if (#equal value #nil)
       (fun _. fail #unit)
       (fun _. #unit);
     defAfter headValue = #head value;
     `t(h) fail headValue;
     defAfter tailValue = #tail value;
     `t(t) fail tailValue)
}